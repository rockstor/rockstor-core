#! /usr/bin/env stap

global nfsd_lookup_clients
global nfsd_read_clients
global nfsd_write_clients
global nfsd_create_clients
global nfsd_commit_clients
global nfsd_remove_clients

global nfsd_clients

probe nfsd.proc.lookup {
	nfsd_lookup_clients[client_ip] <<< 1
	nfsd_clients[client_ip] <<< 1
}

probe nfsd.proc.read {
	nfsd_read_clients[client_ip] <<< size
	nfsd_clients[client_ip] <<< 1
}

probe nfsd.proc.write {
	nfsd_write_clients[client_ip] <<< size
	nfsd_clients[client_ip] <<< 1
}

probe nfsd.proc.create {
	nfsd_create_clients[client_ip] <<< 1
	nfsd_clients[client_ip] <<< 1
}

probe nfsd.proc.commit {
	nfsd_commit_clients[client_ip] <<< 1
	nfsd_clients[client_ip] <<< 1
}

probe nfsd.proc.remove {
	nfsd_remove_clients[client_ip] <<< 1
	nfsd_clients[client_ip] <<< 1
}

probe timer.ms(1000)
{
	tod = gettimeofday_s()
	foreach (ip in nfsd_clients)
	        printf("%d\t%s\t%d\t%d\t%d\t%d\t%d\t%d\t%d\t%d\n",
		tod, ip,
		@count(nfsd_lookup_clients[ip]),
	        @count(nfsd_read_clients[ip]), @count(nfsd_write_clients[ip]),
	        @count(nfsd_create_clients[ip]),
	        @count(nfsd_commit_clients[ip]),
	        @count(nfsd_remove_clients[ip]),
		@sum(nfsd_read_clients[ip]) >> 10,
		@sum(nfsd_write_clients[ip]) >> 10)

	delete nfsd_clients
	delete nfsd_lookup_clients
	delete nfsd_read_clients
	delete nfsd_write_clients
	delete nfsd_create_clients
	delete nfsd_commit_clients
	delete nfsd_remove_clients
}
